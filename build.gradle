buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
        maven { url 'https://jitpack.io' }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
        classpath "com.github.jengelman.gradle.plugins:shadow:4.0.4"
    }
}
plugins {
    id "com.matthewprenger.cursegradle" version "1.1.0"
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.


version = modVersion
group = "de.erdbeerbaerlp.worldManager" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "worldmanager"

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
    options.encoding = 'UTF-8'
}
mainClassName = "de.erdbeerbaerlp.worldManager.Main"
minecraft {
    version = project.forgeVersion
    runDir = "run"

    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = project.mcpVersion
    makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}
repositories {
    jcenter()
    maven { url 'https://jitpack.io' }
    mavenCentral()
}
configurations {
    shade
    compile.extendsFrom shade
}
reobf {
    shadowJar { mappingType = 'SEARGE' }
}
shadowJar {
    classifier = ''
    configurations = [project.configurations.shade]
    relocate "org.apache", "wmshadow.org.apache"
    relocate "org.slf4j", "wmshadow.org.slf4j"
    relocate "org.jsoup", "wmshadow.org.jsoup"
    relocate "org.json", "wmshadow.org.json"
    relocate "com.google.gson", "wmshadow.com.google.gson"
}
dependencies {
    shade group: 'org.json', name: 'json', version: '20180813'
    shade 'com.github.TheRandomLabs:TRLUtils:67595dcc23'
    shade 'com.github.TheRandomLabs:CurseAPI-Minecraft:026882e290'
    shade group: 'commons-io', name: 'commons-io', version: '2.5'
    shade 'com.github.neilwightman:jnbt:master-SNAPSHOT'
    shade 'com.github.zeroturnaround:zt-zip:master'
}
tasks.build.dependsOn reobfShadowJar
processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        // replace version and mcversion
        expand "version": project.version, "mcversion": project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

jar {
    manifest {
        attributes(
                'Maven-Artifact': "${project.group}:${project.archivesBaseName}:${project.version}",
                'Timestamp': System.currentTimeMillis(),
                'Class-Path': configurations.compile.collect { it.getName() }.join(' '),
                'Main-Class': 'de.erdbeerbaerlp.worldManager.Main'
        )
    }
}

curseforge {
    // $GRADLE_USER_HOME/gradle.properties
    apiKey = getProperty("curseforge.apikey")
    project {
        id = '305393'
        changelog = project.changelog
        releaseType = 'beta'
        addGameVersion '1.12.2'
        addGameVersion '1.12.1'
        addGameVersion '1.12'
        addGameVersion '1.11.2'
        addGameVersion '1.11.1'
        addGameVersion '1.11'
        addGameVersion '1.10.2'
        addGameVersion '1.10.1'
        addGameVersion '1.10'
        addGameVersion '1.9.4'
        addGameVersion '1.9.3'
        addGameVersion '1.9.2'
        addGameVersion '1.9.1'
        addGameVersion '1.9'
        addGameVersion '1.8.9'
        addGameVersion '1.8.8'
        addGameVersion '1.8.7'
        addGameVersion '1.8.6'
        addGameVersion '1.8.5'
        addGameVersion '1.8.4'
        addGameVersion '1.8.3'
        addGameVersion '1.8.2'
        addGameVersion '1.8.1'
        addGameVersion '1.8'
        mainArtifact(jar) {
            displayName = "EasyWorldManager-$project.version (MC 1.8-1.12)"
        }
    }
}
tasks.curseforge.dependsOn(build)